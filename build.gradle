buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.9'
    }
}

allprojects {
    group 'io.t28'
    version '0.1.0'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'net.ltgt.errorprone'
}

subprojects {
    def javaVersion = JavaVersion.VERSION_1_8
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'build/classes/main/generated']
        }
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.0.18'

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.7.5'
        testCompile 'org.assertj:assertj-core:3.6.2'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += [
                '-Xep:ImmutableEnumChecker:OFF',
                '-Xep:ConstantField:ERROR',
                '-Xep:MethodCanBeStatic:ERROR',
                '-Xep:MixedArrayDimensions:ERROR',
                '-Xep:MultiVariableDeclaration:ERROR',
                '-Xep:MultipleTopLevelClasses:ERROR',
                '-Xep:PackageLocation:ERROR',
                '-Xep:PrivateConstructorForUtilityClass:ERROR',
                '-Xep:RemoveUnusedImports:ERROR',
                '-Xep:ThrowsUncheckedException:ERROR',
                '-Xep:UnnecessaryStaticImport:ERROR',
                '-Xep:WildcardImport:ERROR'
        ]
    }

    jacoco {
        toolVersion = '0.7.9'
        reportsDir = file("${project.buildDir}/reports/jacoco")
    }

    jacocoTestReport {
        group = 'verification'
        description = 'Generate test coverage reports'
        dependsOn = ['test']

        reports {
            xml.enabled = true
            csv.enabled = false
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport
}
